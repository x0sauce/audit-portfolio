# Minimum and maximum token amount allowed for user is wrongly compared in `updateParticipation`

## Summary

Rova incorrectly compares currency amounts instead of token amounts when validating user allocations limits in `updateParticipation`.

## Vulnerability Details

Rova imposes both minimum and maximum token limits for each user to ensure that their allocation remains within the defined boundaries of the launch group.

However, in `Launch._updateParticipation`, the `userTokenAmount` is incorrectly increase with the currency amount to determine if the user has surpassed or dropped these limits.

The currency amount reflects the amount paid by (or refunded to) the participant in the precision of the ERC20 token (e.g., USDC, which has 6 decimal places) when updating their token allocation.

When participants decrease their token allocation, the userTokenAmount is decrased by the amount refunded in currency e,.(e.g.USDC) instead of the equivalent token amount, which is used to verify if the user's token allocation falls below the `minTokenAmountPerUser` allowed.

When participants increase their token allocation, the userTokenAmount is increased by the amount added in currency e,.(e.g. USDC) instead of the equivalent token amount, which is used to verify if the user's token allocation goes above the `maxTokenAmountPerUser` allowed.

```solidity
    function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        ...
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) { // <@audit currency amount used for comparison instead of token amount
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount);
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) { // <@audit currency amount used for comparison instead of token amount
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount);
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }
        ...

```

## LOC

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L355

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L368

## Root cause

Checks for minimum and maximum allocation uses currency amount instead of token amount

## POC

Consider the following scenario:

- The sale token has 8 decimal places.
- There exists a launch group `0x1` with:
    - `minTokenAmountPerUser` = 10e8
    - `maxTokenAmountPerUser` = 20e8
- The `tokenPriceBps` is set at 1e6 (1 USDC per sale token).
1. Alice currently holds a token allocation of `10e8` linked to a `participationId=0x1`, which was funded with 10 USDC (10e6).
2. She opts to increase her token allocation to `30e8`.
3. Upon entering `Launch.updateParticipation`, and assuming `tokenPriceBps` = 1e6 (1 USDC per sale token), the calculated `newCurrencyAmount` is `30e6` (30 USDC).
4. In the else if block, `additionalCurrencyAmount` is computed as 30e6 - 10e6 = 20e6.
5. Within the nested if block, the check for `maxTokenAmountPerUser` evaluates as `userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser` -> 10e8 + 20e6 > 20e8, which does not trigger a revert.
6. Even though Alice has requested 30e8 tokens, the maximum token allocation check for each user does not revert due to the incorrect variable (`additionalCurrencyAmount`) being utilized.
7. When winners are finalized, the same 30e8 tokens will be applied if she was among the participants.

Similarly, a user can also reduce their token allocation below the minimum token allocation permitted for each user.

## Impact

Participants can bypass minimum and maximum allocations set for each user.

## Mitigation

Compare user allocations with the corresponding amount of tokens to be refunded / added instead of the currency amount

# Token allocation is wrongly updated in `updateParticipation`

## Summary

The user token allocation in `Launch._updateParticipation` is incorrectly updated using currency amounts instead of the equivalent token amounts, causing their funds to be locked.

## Vulnerability Details

In `Launch._updateParticipation`, the user token allocation for each launch group (`userTokens`) is incorrectly increased with the `additionalCurrencyAmount` / decreased with the `refundCurrencyAmount`.

The currency amount reflects the amount paid by (or refunded to) the participant in the precision of the ERC20 token (e.g., USDC, which has 6 decimal places) when updating their token allocation.

When participants decrease their token allocation, the `userTokens` is decreased by the amount refunded in currency e,.(e.g.USDC) instead of the equivalent token amount.

When participants increase their token allocation, the `userTokens` is increased by the amount added in currency e,.(e.g. USDC) instead of the equivalent token amount.

```solidity
    function updateParticipation(UpdateParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        ...
        if (prevInfo.currencyAmount > newCurrencyAmount) {
            // Calculate refund amount
            uint256 refundCurrencyAmount = prevInfo.currencyAmount - newCurrencyAmount;
            // Validate user new requested token amount is greater than min token amount per user
            if (userTokenAmount - refundCurrencyAmount < settings.minTokenAmountPerUser) {
                revert MinUserTokenAllocationNotReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount - refundCurrencyAmount); <@audit currency amount used instead of token amount
            // Transfer payment currency from contract to user
            IERC20(request.currency).safeTransfer(msg.sender, refundCurrencyAmount);
        } else if (newCurrencyAmount > prevInfo.currencyAmount) {
            // Calculate additional payment amount
            uint256 additionalCurrencyAmount = newCurrencyAmount - prevInfo.currencyAmount;
            // Validate user new requested token amount is within launch group user allocation limits
            if (userTokenAmount + additionalCurrencyAmount > settings.maxTokenAmountPerUser) {
                revert MaxUserTokenAllocationReached(
                    request.launchGroupId, request.userId, userTokenAmount, request.tokenAmount
                );
            }
            // Update total tokens requested for user for launch group
            userTokens.set(request.userId, userTokenAmount + additionalCurrencyAmount); <@audit currency amount used instead of token amount
            // Transfer payment currency from user to contract
            IERC20(request.currency).safeTransferFrom(msg.sender, address(this), additionalCurrencyAmount);
        }
        ...

```

This leads to future attempts by participants to cancel their participation or request a refund to being unsuccessful.

In `Launch.cancelParticipation`

```solidity
    function cancelParticipation(CancelParticipationRequest calldata request, bytes calldata signature)
        external
        nonReentrant
        whenNotPaused
        onlyLaunchGroupStatus(request.launchGroupId, LaunchGroupStatus.ACTIVE)
    {
        ...
        if (userTokenAmount - info.tokenAmount == 0) { <@audit reverts due to underflow
            // If total tokens requested for user is the same as the cancelled participation, remove user from launch group
            userTokens.remove(request.userId);
        } else if (userTokenAmount - info.tokenAmount < settings.minTokenAmountPerUser) { <@audit reverts due to underflow
            // Total tokens requested for user after cancellation must be greater than min token amount per user
            revert MinUserTokenAllocationNotReached(
                request.launchGroupId, request.userId, userTokenAmount, info.tokenAmount
            );
        } else {
            // Subtract cancelled participation token amount from total tokens requested for user
            userTokens.set(request.userId, userTokenAmount - info.tokenAmount); <@audit reverts due to underflow
        }

        ...
    }
```

In `Launch._processRefund`

```solidity
    function _processRefund(bytes32 launchGroupId, bytes32 launchParticipationId, ParticipationInfo storage info)
        private
    {
        // If participation is finalized or has no currency amount or no token amount (cancelled), revert
        if (info.isFinalized || info.currencyAmount == 0 || info.tokenAmount == 0) {
            revert InvalidRefundRequest(launchParticipationId, info.userId);
        }

        // Subtract refunded participation token amount from total tokens requested for user
        EnumerableMap.Bytes32ToUintMap storage userTokens = _userTokensByLaunchGroup[launchGroupId];
        (, uint256 userTokenAmount) = userTokens.tryGet(info.userId);
        userTokens.set(info.userId, userTokenAmount - info.tokenAmount); <@audit reverts due to underflow

        // Reset participation info
        uint256 refundCurrencyAmount = info.currencyAmount;
        info.tokenAmount = 0;
        info.currencyAmount = 0;

        // Transfer payment currency from contract to user
        IERC20(info.currency).safeTransfer(info.userAddress, refundCurrencyAmount);

        emit RefundClaimed(
            launchGroupId, launchParticipationId, info.userId, info.userAddress, refundCurrencyAmount, info.currency
        );
    }
```

## LOC

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L361

https://github.com/sherlock-audit/2025-02-rova/blob/main/rova-contracts/src/Launch.sol#L374

## Root cause

User token allocations for each launch group are being inaccurately adjusted based on the currency amount.

## POC

Consider the following scenario:

- The sale token has 8 decimal places.
- There exists a launch group `0x1` with:
    - `minTokenAmountPerUser` = 10e8
    - `maxTokenAmountPerUser` = 20e8
- The `tokenPriceBps` is set at 1e6 (1 USDC per sale token).
1. Alice has a token allocation of `10e8` associated with `participationId=0x1`, which was funded with 10 USDC (10e6) when she executed `Launch.participation`.
2. She decides to increase her token allocation to `30e8`.
3. When she enters `Launch.updateParticipation`, and assuming `tokenPriceBps` is set to 1e6 (1 USDC per sale token), the computed `newCurrencyAmount` becomes `30e6` (30 USDC).
4. In the else if block, the `additionalCurrencyAmount` is calculated as 30e6 - 10e6 = 20e6.
5. Her token allocation for the launch group is updated using `userTokens.set(request.userId, 10e8 + 20e6)`.
6. She successfully modifies her participation from (`participationId` changing from 0x1 to 0x2) with a `tokenAmount` of `30e8`.
7. After a while, she chooses to cancel her participation of `30e8` tokens while the launch group remains active.
8. When she calls `Launch.cancelParticipation`, it will revert because `userTokenAmount - info.tokenAmount = (10e8 + 20e6) - 30e8` will cause an underflow.
9. As a result, Alice is unable to cancel her position, and her funds remain locked in the `Launch` contract.

A similar scenario would happen when the user attempts to claim back their refunds.

## Impact

Participants funds are locked in Launch contract.

This also breaks the invariant stated in the ReadMe

> The rest of the payment currency tokens for “unfinalized” participations should be available for user refund.
> 

## Mitigation

Update user allocations with the corresponding amount of tokens to be refunded / added instead of the currency amount